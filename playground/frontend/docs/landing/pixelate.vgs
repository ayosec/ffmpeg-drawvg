setvar SIZE 20

setvar lastcolor 0

repeat (h / SIZE + 1) {
    save

    if (mod(i, 2)) {
        translate SIZE 0
    }

    repeat (w / SIZE + 1) {
        setvar cr 0
        setvar cg 0
        setvar cb 0

        setvar cc 0

        repeat 10 {
            setvar d ((i - 5) * SIZE / 5)
            setvar px (p(0, d))

            if (not(isnan(px))) {
                setvar cc (cc + 1)
                setvar cr (cr + px / 0x1000000)
                setvar cg (cg + bitand(px / 0x10000, 0xFF))
                setvar cb (cb + bitand(px / 0x100, 0xFF))
            }
        }

        if (not(cc)) {
            setcolor lastcolor
        }

        if cc {
            defrgba curcolor (cr / cc / 255) (cg / cc / 255) (cb / cc / 255) 1
            setcolor curcolor
            setvar lastcolor curcolor
        }

        moveto (-SIZE) 0
        lineto 0 (-SIZE), SIZE 0, 0 SIZE
        closepath

        preserve
        fill

        setcolor black@0.1
        stroke

        translate (SIZE * 2) 0
    }

    restore

    translate 0 SIZE
}
